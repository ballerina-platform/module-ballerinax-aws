# Ballerina AWS library

[![Build](https://github.com/ballerina-platform/module-ballerinax-aws/actions/workflows/ci.yml/badge.svg)](https://github.com/ballerina-platform/module-ballerinax-aws/actions/workflows/ci.yml)
[![codecov](https://codecov.io/gh/ballerina-platform/module-ballerinax-aws/branch/master/graph/badge.svg)](https://codecov.io/gh/ballerina-platform/module-ballerinax-aws)
[![Trivy](https://github.com/ballerina-platform/module-ballerinax-aws/actions/workflows/trivy-scan.yml/badge.svg)](https://github.com/ballerina-platform/module-ballerinax-aws/actions/workflows/trivy-scan.yml)
[![GraalVM Check](https://github.com/ballerina-platform/module-ballerinax-aws/actions/workflows/build-with-bal-test-graalvm.yml/badge.svg)](https://github.com/ballerina-platform/module-ballerinax-aws/actions/workflows/build-with-bal-test-graalvm.yml)
[![GitHub Last Commit](https://img.shields.io/github/last-commit/ballerina-platform/module-ballerinax-aws.svg)](https://github.com/ballerina-platform/module-ballerinax-aws/commits/master)

## Overview

[AWS](https://aws.amazon.com/) is a comprehensive cloud computing platform offering over 200 services, all authenticated through a common scheme: IAM credentials and [AWS Signature Version 4](https://docs.aws.amazon.com/IAM/latest/UserGuide/reference_aws-signing.html) request signing. This library implements that scheme once, so it does not need to be re-implemented per AWS service.

The library has two modules: the root module (`ballerinax/aws`) with the `Region` type and region-based endpoint resolution, and the `auth` submodule (`ballerinax/aws.auth`) with credential resolution across all standardized AWS credential sources and AWS Signature Version 4 request signing. It is the authentication foundation used by the `ballerinax/aws.*` connectors, and can also be used directly to call AWS services that do not have a dedicated connector yet.

## Credential resolution

The `auth:CredentialProvider` class resolves AWS credentials from a configured source and caches them, refreshing expiring temporary credentials (STS, SSO, instance-profile) automatically. Create one per configured source, at initialization, and reuse it for the lifetime of the application:

```ballerina
import ballerinax/aws.auth;

auth:CredentialProvider credProvider = check new (auth:DEFAULT_CREDENTIALS);
```

Fetch credentials per request; expiring temporary credentials are renewed transparently:

```ballerina
auth:Credentials credentials = check credProvider.getCredentials();
```

Release the provider's resources (background refresh threads, HTTP connections for STS/SSO) when it is no longer needed:

```ballerina
check credProvider.close();
```

### Credential sources

`auth:CredentialProvider` accepts any of the following configurations as its `AuthConfig`.

#### Default credential provider chain

Resolves credentials automatically from the AWS SDK's default chain (environment variables, `~/.aws/credentials`, an EC2/ECS/EKS instance role, etc.) — no configuration needed. This is the preferred source when running on AWS infrastructure.

```ballerina
auth:CredentialProvider credProvider = check new (auth:DEFAULT_CREDENTIALS);
```

#### Static credentials

The least preferred source for production use; prefer one of the sources below when possible.

```ballerina
auth:CredentialProvider credProvider = check new ({
    accessKeyId: "AKIA...",
    secretAccessKey: "..."
});
```

> **Tip:** To create an access key, sign in to the [AWS Management Console](https://console.aws.amazon.com/console), open **IAM** → **Users** → create or select a user → **Security credentials** tab → **Create access key**.

#### Profile-based authentication

Reads credentials from a named profile in a local AWS credentials file (as created by `aws configure`).

```ballerina
auth:CredentialProvider credProvider = check new ({
    profileName: "dev"
});
```

#### STS assume-role

Obtains temporary credentials by assuming an IAM role via AWS STS.

```ballerina
auth:CredentialProvider credProvider = check new ({
    roleArn: "arn:aws:iam::123456789012:role/reporting-read-only",
    sourceCredentials: auth:DEFAULT_CREDENTIALS
});
```

#### Web identity (EKS IRSA, CI/CD OIDC)

Exchanges a web identity (OIDC) token for temporary credentials via AWS STS.

```ballerina
auth:CredentialProvider credProvider = check new ({
    roleArn: "arn:aws:iam::123456789012:role/order-service",
    webIdentityTokenFile: "/var/run/secrets/eks.amazonaws.com/serviceaccount/token"
});
```

#### IAM Identity Center (SSO)

Requires an active session created with `aws sso login`.

```ballerina
auth:CredentialProvider credProvider = check new ({
    ssoStartUrl: "https://myorg.awsapps.com/start",
    ssoRegion: "us-east-1",
    accountId: "123456789012",
    roleName: "DeveloperAccess"
});
```

#### External credential process

Executes an external command implementing the AWS `credential_process` contract, which must print a JSON credential document to stdout.

```ballerina
auth:CredentialProvider credProvider = check new ({
    command: ["/usr/local/bin/aws_signing_helper", "credential-process", "--certificate", "/path/to/cert.pem"]
});
```

> **Security note:** the configured command runs with the privileges of the running program. Prefer declaring it in `~/.aws/config` (`credential_process`) and using profile-based authentication or the default credential provider chain where possible.

## Region and endpoint resolution

`aws:Region` is a typed enum of all AWS regions. Endpoints are resolved using the AWS SDK's own endpoint metadata, covering all partitions and the FIPS/dualstack variants, with a standard-pattern fallback for regions newer than the bundled SDK:

```ballerina
import ballerinax/aws;

string url = aws:resolveEndpoint("events", aws:US_EAST_1);
// "https://events.us-east-1.amazonaws.com"

string host = aws:resolveEndpointHost("events", aws:US_EAST_1);
// "events.us-east-1.amazonaws.com"
```

`EndpointConfig` selects the FIPS or dualstack variant, or overrides the endpoint entirely — useful for local testing against [LocalStack](https://www.localstack.cloud/) or similar:

```ballerina
string testUrl = aws:resolveEndpoint("events", aws:US_EAST_1, {customEndpoint: "http://localhost:4566"});
// "http://localhost:4566"

string fipsUrl = aws:resolveEndpoint("events", aws:US_EAST_1, {fips: true});
// "https://events-fips.us-east-1.amazonaws.com"
```

## Request signing

`auth:getSignedHeaders` signs a request with AWS Signature Version 4 and returns the headers to set on the outbound request. Use it to call any AWS service, including ones without a dedicated Ballerina connector.

The following example calls Amazon EventBridge's `PutEvents` over a plain `http:Client`:

```ballerina
import ballerina/http;
import ballerinax/aws;
import ballerinax/aws.auth;

public function main() returns error? {
    auth:CredentialProvider credProvider = check new (auth:DEFAULT_CREDENTIALS);
    string host = aws:resolveEndpointHost("events", aws:US_EAST_1);
    http:Client eventBridge = check new ("https://" + host);

    json putEventsRequest = {
        "Entries": [
            {
                "Source": "com.mycompany.orders",
                "DetailType": "OrderPlaced",
                "Detail": {"orderId": "o-1234"}.toJsonString()
            }
        ]
    };
    byte[] payload = putEventsRequest.toJsonString().toBytes();

    auth:Credentials credentials = check credProvider.getCredentials();
    map<string> signedHeaders = check auth:getSignedHeaders({
        method: "POST",
        host,
        headers: {
            "content-type": "application/x-amz-json-1.1",
            "x-amz-target": "AWSEvents.PutEvents"
        },
        payload
    }, credentials, aws:US_EAST_1, "events");

    http:Request request = new;
    request.setBinaryPayload(payload);
    foreach [string, string] [name, value] in signedHeaders.entries() {
        request.setHeader(name, value);
    }
    http:Response response = check eventBridge->execute("POST", "/", request);
}
```

## Errors

`auth:Error` is the base error type for the `auth` module. `auth:CredentialResolutionError` is returned when a configured credential source cannot supply credentials — its `aws:ErrorDetails` are populated when the failure originates from an AWS service call (e.g. STS or SSO). `auth:SigningError` is returned when signing a request fails.

## Issues and projects

Issues and Projects tabs are disabled for this repository as this is part of the Ballerina library extended ecosystem. To report bugs, request new features, start new discussions, view project boards, etc., go to the [Ballerina Library repository](https://github.com/ballerina-platform/ballerina-library).

This repository only contains the source code for the package.

## Build from the source

### Prerequisites

1. Download and install Java SE Development Kit (JDK) version 21. You can download it from either of the following sources:
    * [Oracle JDK](https://www.oracle.com/java/technologies/downloads/)
    * [OpenJDK](https://adoptium.net/)

    > **Note:** Set the `JAVA_HOME` environment variable to the path name of the directory into which you installed JDK.

2. Download and install [Ballerina Swan Lake](https://ballerina.io/).
3. Download and install [Docker](https://www.docker.com/get-started).
4. Generate a GitHub access token with read package permissions, then set the following `env` variables:

    ```shell
    export packageUser=<Your GitHub Username>
    export packagePAT=<GitHub Personal Access Token>
    ```

### Build options

Execute the commands below to build from the source.

1. To build the package:
   ```bash
   ./gradlew clean build
   ```
2. To run the tests:
   ```bash
   ./gradlew clean test
   ```
3. To run a group of tests:
   ```bash
   ./gradlew clean test -Pgroups=<test_group_names>
   ```
4. To build without the tests:
   ```bash
   ./gradlew clean build -x test
   ```
5. To debug the package with a remote debugger:
   ```bash
   ./gradlew clean build -Pdebug=<port>
   ```
6. To debug with Ballerina language:
   ```bash
   ./gradlew clean build -PbalJavaDebug=<port>
   ```
7. To publish to the local Ballerina Central repository:
   ```bash
   ./gradlew clean build -PpublishToLocalCentral=true
   ```
8. To publish to Ballerina Central:
   ```bash
   ./gradlew clean build -PpublishToCentral=true
   ```

## Contribute to Ballerina

As an open-source project, Ballerina welcomes contributions from the community.

For more information, go to the [contribution guidelines](https://github.com/ballerina-platform/ballerina-lang/blob/master/CONTRIBUTING.md).

## Code of conduct

All the contributors are encouraged to read the [Ballerina Code of Conduct](https://ballerina.io/code-of-conduct).

## Useful links

* For more information, go to the [`aws` package](https://central.ballerina.io/ballerinax/aws/latest).
* For example demonstrations of the usage, go to [Ballerina By Examples](https://ballerina.io/learn/by-example/).
* Chat live with us via our [Discord server](https://discord.gg/ballerinalang).
* Post all technical questions on Stack Overflow with the [#ballerina](https://stackoverflow.com/questions/tagged/ballerina) tag.
